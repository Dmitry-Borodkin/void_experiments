{   v_import("level-00");
    v_import("level-01");
    v_import("level-02");

    v_import("llvm-c/Support.void");

    v_import("cairo.void");
}

{   v_import("printf.void");
}

{   v_enable_level_01();
    v_enable_level_02();
}


//---------------------------------------------------------------------
//- ... <cairo/cairo.h>
//---------------------------------------------------------------------
{   ok = LLVMLoadLibraryPermanently("libcairo.so");

    printf("ok: %d\n", ok);
}


//---------------------------------------------------------------------
double = float(64);


//---------------------------------------------------------------------
{
    cairo_ptr         = v_pointer_type(cairo_t,         0);
    cairo_surface_ptr = v_pointer_type(cairo_surface_t, 0);

    q = v_quark_from_string;

    v_util_function_dict_set(q(".flush()"),        cairo_surface_ptr, "cairo_surface_flush");
    v_util_function_dict_set(q(".write_to_png()"), cairo_surface_ptr, "cairo_surface_write_to_png");
    v_util_function_dict_set(q(".destroy()"),      cairo_surface_ptr, "cairo_surface_destroy");

    v_util_function_dict_set(q(".destroy()"),         cairo_ptr, "cairo_destroy");
    v_util_function_dict_set(q(".paint()"),           cairo_ptr, "cairo_paint");
    v_util_function_dict_set(q(".fill()"),            cairo_ptr, "cairo_fill");
    v_util_function_dict_set(q(".fill_preserve()"),   cairo_ptr, "cairo_fill_preserve");
    v_util_function_dict_set(q(".stroke()"),          cairo_ptr, "cairo_stroke");
    v_util_function_dict_set(q(".set_source_rgb()"),  cairo_ptr, "cairo_set_source_rgb");
    v_util_function_dict_set(q(".set_source_rgba()"), cairo_ptr, "cairo_set_source_rgba");
    v_util_function_dict_set(q(".rectangle()"),       cairo_ptr, "cairo_rectangle");
    v_util_function_dict_set(q(".set_line_width()"),  cairo_ptr, "cairo_set_line_width");
    v_util_function_dict_set(q(".line_to()"),         cairo_ptr, "cairo_line_to");
    v_util_function_dict_set(q(".move_to()"),         cairo_ptr, "cairo_move_to");
    v_util_function_dict_set(q(".close_path()"),      cairo_ptr, "cairo_close_path");
    v_util_function_dict_set(q(".set_fill_rule()"),   cairo_ptr, "cairo_set_fill_rule");
    v_util_function_dict_set(q(".set_operator()"),    cairo_ptr, "cairo_set_operator");
    v_util_function_dict_set(q(".get_operator()"),    cairo_ptr, "cairo_get_operator");
}


//---------------------------------------------------------------------
rand: () -> int;

RAND_MAX = 2147483647;      //- ?

my_rand: () -> double
{
    v_return( (rand(): double) / RAND_MAX );
}


//---------------------------------------------------------------------
sin: (double) -> double;
cos: (double) -> double;

M_PI = 3.14159265358979323846264338327950288419716939937510582;


//---------------------------------------------------------------------
random_triangle: (cr: *cairo_t, w: int, h: int) -> void
{
    t = 2 * my_rand();

    t = t*t*(3 - t) / 2;
    t = t*t*(3 - t) / 2;

    sh = t*t*(3 - t) / 4 * (w - h);

    x0 = sh + h*my_rand();
    y0 =      h*my_rand();

    x1 = sh + h*my_rand();
    y1 =      h*my_rand();

    x2 = sh + h*my_rand();
    y2 =      h*my_rand();

    c0 = my_rand();
    c1 = my_rand();

    a0 = my_rand();     a0 = a0 * a0 * 0.8;
    a1 = my_rand();     a1 = a1 * a1 * 0.8;


    cr.move_to(x0, y0);
    cr.line_to(x1, y1);
    cr.line_to(x2, y2);
    cr.close_path();

    cr.set_source_rgba(c0, c0, c0, a0);

    cr.fill_preserve();

    cr.set_source_rgba(c1, c1, c1, a1);

    cr.stroke();
}

//---------------------------------------------------------------------
nod: (N: int, M: int) -> int
{
    n: &int = N;
    m: &int = M;

    loop
    {
        if (n < m)  { s: int = n; n := m; m := s; }

        r = n % m;

        if (r == 0) v_return(m);

        n := r;
    }
}

//---------------------------------------------------------------------
random_star: (cr: *cairo_t, w: int, h: int) -> void
{
    r = my_rand();

    kr = 50;

    r = r / ((1-kr)*r + kr);

    r = r * h / 2;

    x = my_rand();
    y = my_rand();

//    x = x*(w - 2*r) + r;
//    y = y*(h - 2*r) + r;

    x = x * w;
    y = y * h;

    n = my_rand();

    kn = 5;

    n = n / ((1-kn)*n + kn);

    n = (n*20 : int) + 3;

    m: &int := v_undef();

    loop
    {
        v = my_rand();

        m := (v*n/2 : int);

        if (m == 0) v_continue();

        if (nod(n, m) == 1) v_break();
    }

    TAU = 2*M_PI;

    A = my_rand();

    A = A * TAU / n;

    D = TAU / n * m;

    a: &double := A;

    for (i: &int := 0; i < n; ++i)
    {
        vx = x + r*sin(a);
        vy = y + r*cos(a);

        if (i == 0) cr.move_to(vx, vy);
        else        cr.line_to(vx, vy);

        a += D;
    }

    cr.close_path();


    c0 = my_rand();
    c1 = my_rand();

    a0 = my_rand();     a0 = a0 * a0 * 0.6;
    a1 = my_rand();     a1 = a1 * a1 * 0.6;


    cr.set_source_rgba(c0, c0, c0, a0);

    cr.fill_preserve();

    cr.set_source_rgba(c1, c1, c1, a1);

    cr.stroke();
}

//---------------------------------------------------------------------
//{ v_debug_print_module(1); }
{
    w = 1500;
    h =  500;

    surface = cairo_image_surface_create(CAIRO_FORMAT_RGB24, w, h);
    defer surface.destroy();

    {   cr = cairo_create(surface);
        defer cr.destroy();

        cr.set_source_rgb(0, 0, 0);
        cr.paint();

        cr.set_line_width(1);

        cr.set_fill_rule(CAIRO_FILL_RULE_EVEN_ODD);

        cr.set_operator(CAIRO_OPERATOR_HARD_LIGHT);

        for (i: &int := 0; i < 10000; ++i)
        {
//          random_triangle(cr, w, h);
            random_star(cr, w, h);
        }
    }

    surface.flush();

    surface.write_to_png("try_cairo4_02.png");
}


//---------------------------------------------------------------------
system: (*const char) -> int;


{   system("display try_cairo4_02.png");
}


