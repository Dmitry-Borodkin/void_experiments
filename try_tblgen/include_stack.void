{   v_import("imports.void"); }

{   enable_imports(); }

{   v_set_export_flag(true); }


//---------------------------------------------------------------------
{   voidc_unit_begin_module("include_stack_mod"); }


//---------------------------------------------------------------------
//- include_stack_node_t ...
//---------------------------------------------------------------------
private
struct include_stack_node_t
{
    file:  *FILE;                           //- File opened
    count: int;                             //- ifdef counter
    next:  *include_stack_node_t;           //- Next node ...
};

//---------------------------------------------------------------------
private
include_stack: &*include_stack_node_t := 0;


//---------------------------------------------------------------------
//- Include stack utility...
//---------------------------------------------------------------------
include_stack_push: (f: *FILE) ~> void
{
    include_stack := new include_stack_node_t {{ f, 0, include_stack }};
}

//---------------------------------------------------------------------
include_stack_pop: () ~> void
{
    node = *include_stack;

    include_stack := node.next;

    delete &node;
}

//---------------------------------------------------------------------
include_stack_peek: () ~> *FILE
{
    node = *include_stack;

    v_return(node.file);
}

//---------------------------------------------------------------------
include_stack_peek_ifdef_counter: () ~> &int
{
    node = *include_stack;

    v_return(node.count);
}

//---------------------------------------------------------------------
include_stack_empty: () ~> bool
{
    v_return(include_stack == 0);
}


//---------------------------------------------------------------------
//- ...
//---------------------------------------------------------------------
{   voidc_unit_end_module(); }


