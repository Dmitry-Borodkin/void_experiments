{   v_import("mainline.void");

    v_import("llvm-c/Support.void");

    v_import("printf.void");

    v_import("cairo.void");
}

{   v_enable_mainline(); }


//---------------------------------------------------------------------
//- ... <gtk/gtk.h>
//---------------------------------------------------------------------
{
//  ok = LLVMLoadLibraryPermanently("libgtk-4.so");
    ok = LLVMLoadLibraryPermanently("libgtk-4-1.dll");

//  printf("ok: %d\n", ok);
}

//---------------------------------------------------------------------
struct GtkApplication;
struct GtkWidget;
struct GtkDrawingArea;

//---------------------------------------------------------------------
gtk_application_new: (id: *const char, flags: int) ~> *GtkApplication;

g_signal_connect_data: (instance: *void,
                        signal: *const char,
                        callback: *(()~>void),
                        data: *void,
                        notify: *void,
                        flags: int
                       ) ~> long;

g_application_run: (app: *GtkApplication, argc: int, argv: **char) ~> int;


gtk_application_window_new: (app: *GtkApplication) ~> *GtkWidget;


gtk_window_set_child: (wnd: *GtkWidget, widget: *GtkWidget) ~> void;

gtk_window_present: (wnd: *GtkWidget) ~> void;


gtk_drawing_area_new: () ~> *GtkWidget;


GtkDrawingAreaDrawFunc = *((*GtkDrawingArea, *cairo_t, w: int, h: int, aux: *void) ~> void);

gtk_drawing_area_set_draw_func: (*GtkDrawingArea, GtkDrawingAreaDrawFunc, aux: *void, destroy: *void) ~> void;


gtk_widget_queue_draw: (*GtkWidget) ~> void;


//---------------------------------------------------------------------
on_idle: (aux: *void) ~> bool
{
    gtk_widget_queue_draw((aux: *GtkWidget));

    v_return(false);
}

g_idle_add: (*((*void)~>bool), *void) ~> unsigned;


//---------------------------------------------------------------------
draw_mandel: (drawing_area: *GtkDrawingArea,
              cr: *cairo_t,
              width: int,
              height: int,
              aux: *void
             ) ~> void;

//---------------------------------------------------------------------
on_activate: (app: *GtkApplication, data: *void) ~> void
{
    window = gtk_application_window_new(app);

    drawing_area = gtk_drawing_area_new();

    gtk_drawing_area_set_draw_func((drawing_area: *GtkDrawingArea), draw_mandel, 0, 0);

    gtk_window_set_child(window, drawing_area);

    gtk_window_present(window);
}


//=====================================================================
double = float(64);

struct complex
{
    vec(double, 2);
};

alwaysinline  (_.re): (a:  complex) ~>  double  { v_return(a[0][0]); }
alwaysinline  (_.re): (a: &complex) ~> &double  { v_return(a[0][0]); }

alwaysinline  (_.im): (a:  complex) ~>  double  { v_return(a[0][1]); }
alwaysinline  (_.im): (a: &complex) ~> &double  { v_return(a[0][1]); }


//---------------------------------------------------------------------
alwaysinline
norm: (a: complex) ~> double
{
    v_return(a.re*a.re + a.im*a.im);
}

//---------------------------------------------------------------------
alwaysinline
(_+_): (a: complex, b: complex) ~> complex
{
    v_return({a[0] + b[0]});
}

alwaysinline
(_-_): (a: complex, b: complex) ~> complex
{
    v_return({a[0] - b[0]});
}

alwaysinline
(_*_): (a: complex, b: complex) ~> complex
{
    v_return({{a.re*b.re - a.im*b.im, a.im*b.re + a.re*b.im}});
}

alwaysinline
(_/_): (a: complex, b: complex) ~> complex
{
    d = norm(b);

    v_return({ {(a.re*b.re + a.im*b.im)/d, (a.im*b.re - a.re*b.im)/d} });
}

//---------------------------------------------------------------------
alwaysinline
(_+=_): (a: &complex, b: complex) ~> &complex
{
    a[0] += b[0];

    v_return(a);
}

alwaysinline
(_-=_): (a: &complex, b: complex) ~> &complex
{
    a[0] -= b[0];

    v_return(a);
}

alwaysinline
(_*=_): (a: &complex, b: complex) ~> &complex
{
    a := a * b;

    v_return(a);
}

alwaysinline
(_/=_): (a: &complex, b: complex) ~> &complex
{
    a := a / b;

    v_return(a);
}


//=====================================================================
MAXITER = 10000000;

EPSILON = 1e-16;

//---------------------------------------------------------------------
check_mandel: (c: complex) ~> int
{
    p: &complex := 0;

    period: &int := 0;

    maxperiod: &int := 2;

    z: &complex := c;

    for (i: &int := 0; i < MAXITER; ++i)
    {
        if (norm(z) > 4)  v_return(i);

        z := z*z + c;

        eps = norm(p - z);

        if (eps < EPSILON)  v_return(-1);

        ++period;

        if (period > maxperiod)
        {
            period := 0;

            maxperiod += maxperiod/2;

            p := z;
        }
    }

    v_return(-2);       //- Sic!
}


//---------------------------------------------------------------------
check_mandel2: (c: complex) ~> int
{

    p: &complex := 0;

    z: &complex := c;

    for (i: &int := 0; i < MAXITER; ++i)
    {
        if (norm(z) > 4)  v_return(i);

        z := z*z + c;

        eps = norm(p - z);

        if (eps < EPSILON)  v_return(-1);
//      if (eps < EPSILON)  v_return(i);

        if (i & 1)  p := p*p + c;
    }

    v_return(-2);       //- Sic!
}


////---------------------------------------------------------------------
//center: complex = {{ -0.5, 0.0 }};
//
//scale: double = 1.5;

center: complex = {{ -1.0 + 0.8*0.25, 0.6*0.25 }};

scale: double = 1/1e3;


//---------------------------------------------------------------------
sqrt: (double) ~> double;


//---------------------------------------------------------------------
wsize: &unsigned[2] := 0;
wline: &unsigned := v_undef();

data: &(*uint(8)) := 0;     defer v_free(data);

stride: &unsigned := v_undef();


//=====================================================================
draw_mandel: (drawing_area: *GtkDrawingArea,
              cr: *cairo_t,
              width: int,
              height: int,
              aux: *void
             ) ~> void
{
    w = wsize[0];
    h = wsize[1];

    if (w != width  ||  h != height)
    {
        v_free(data);

        stride := cairo_format_stride_for_width(CAIRO_FORMAT_RGB24, width);

        data := v_malloc(uint(8), stride*height);

        w := width;
        h := height;

        wline := 0;
    }

    {   k = 2*scale / height;

        x0 = center.re - k*(width -1)/2;
        y0 = center.im + k*(height-1)/2;

        z0: complex = {{x0, y0}};

        j = wline;

        if (j < height)
        {
            d_j = (data + stride*j : *uint(8)[4]);

            y = k * j;

            for (i: &int := 0; i < width; ++i)
            {
                x = k * i;

                d = check_mandel(z0 + {{x, -y}});

                p = *(d_j + i : *vec(uint(8), 3));

                if (d >= 0)
                {
                    u = (d + (d&1)*16) % 256;

                    p := (sqrt(u / 255.0) * 255 : uint(8));
                }
                else
                {
                    if (d == -1)  p := 0;
                    else          p := {0, 0, 255};
                }
            }

            ++j;

            g_idle_add(on_idle, drawing_area);
        }
    }

    surface = cairo_image_surface_create_for_data(data, CAIRO_FORMAT_RGB24, width, height, stride);
    defer cairo_surface_destroy(surface);

    cairo_save(cr);
    defer cairo_restore(cr);

    cairo_set_source_surface(cr, surface, 0, 0);

    cairo_paint(cr);
}


//---------------------------------------------------------------------
//{ v_debug_print_module(1); }
{
    app = gtk_application_new(0, 0);

    g_signal_connect_data(app, "activate", (on_activate: *(()~>void)), 0, 0, 0);

    g_application_run(app, 1, &("Нечто" : *char));
}




